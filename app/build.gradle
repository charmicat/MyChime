plugins {
    id('com.android.application')
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'org.jetbrains.kotlin.android'
}

// Creates a variable called keystorePropertiesFile, and initializes it to the
// keystore.properties file.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initializes a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
def hasCustomKeystore = false
// Loads the keystore.properties file into the keystoreProperties object.
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    hasCustomKeystore = true
}

android {
    compileSdk 33

    signingConfigs {
        debug {
            if (hasCustomKeystore) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    namespace 'com.vag.mychime'

    defaultConfig {
        applicationId 'com.vag.mychime'
        minSdk 23
        targetSdk 33
        versionCode 19
        versionName '6.0'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        if (hasCustomKeystore) {
            signingConfig signingConfigs.debug
        }
        proguardFiles getDefaultProguardFile('proguard-android.txt')
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:34.4.0')

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.navigation:navigation-fragment:2.9.5'
    implementation 'androidx.navigation:navigation-ui:2.9.5'
    implementation 'io.github.charmicat:vaghelper:2.0.1'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation('androidx.test:monitor:1.8.0')
    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.7.0'
    testImplementation 'androidx.test.ext:junit:1.3.0'
// Robolectric environment
    testImplementation 'org.robolectric:robolectric:4.16'
// Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:5.20.0'
}